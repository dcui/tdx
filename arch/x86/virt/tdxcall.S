/* SPDX-License-Identifier: GPL-2.0 */
#include <asm/asm-offsets.h>
#include <asm/tdx.h>

/*
 * TDX guests use the TDCALL instruction to make requests to the
 * TDX module and hypercalls to the VMM.
 *
 * TDX host user SEAMCALL instruction to make requests to TDX module.
 *
 * They are supported in Binutils >= 2.36.
 */
#define tdcall		.byte 0x66,0x0f,0x01,0xcc
#define seamcall	.byte 0x66,0x0f,0x01,0xcf

.macro TDX_MODULE_CALL host:req error_check=1
	/*
	 * R12 will be used as temporary storage for struct tdx_module_output
	 * pointer. Since R12-R15 registers are not used by TDCALL/SEAMCALL
	 * services supported by this function, it can be reused.
	 */

	/* Callee saved, so preserve it */
	push %r12

	/*
	 * Push output pointer to stack.
	 * After the operation, it will be fetched into R12 register.
	 */
	push %r9

	/* Mangle function call ABI into TDCALL/SEAMCALL ABI: */
	/* Move Leaf ID to RAX */
	mov %rdi, %rax
	/* Move input 4 to R9 */
	mov %r8,  %r9
	/* Move input 3 to R8 */
	mov %rcx, %r8
	/* Move input 1 to RCX */
	mov %rsi, %rcx
	/* Leave input param 2 in RDX */

	.if \host
	seamcall
	/*
	 * SEAMCALL instruction is essentially a VMExit from VMX root
	 * mode to SEAM VMX root mode.  VMfailInvalid (CF=1) indicates
	 * that the targeted SEAM firmware is not loaded or disabled,
	 * or P-SEAMLDR is busy with another SEAMCALL.  %rax is not
	 * changed in this case.
	 *
	 * Set %rax to TDX_SEAMCALL_VMFAILINVALID for VMfailInvalid.
	 * This value will never be used as actual SEAMCALL error code.
	*/
	.if \error_check
	jnc .Lno_vmfailinvalid
	mov $TDX_SEAMCALL_VMFAILINVALID, %rax
	.endif
.Lno_vmfailinvalid:
	.else
	tdcall
	.endif

	/*
	 * Fetch output pointer from stack to R12 (It is used
	 * as temporary storage)
	 */
	pop %r12

	/* Check for success: 0 - Successful, otherwise failed */
	.if \error_check
	test %rax, %rax
	jnz .Lno_output_struct
	.endif

	/*
	 * Since this function can be initiated without an output pointer,
	 * check if caller provided an output struct before storing
	 * output registers.
	 */
	test %r12, %r12
	jz .Lno_output_struct

	/* Copy result registers to output struct: */
	movq %rcx, TDX_MODULE_rcx(%r12)
	movq %rdx, TDX_MODULE_rdx(%r12)
	movq %r8,  TDX_MODULE_r8(%r12)
	movq %r9,  TDX_MODULE_r9(%r12)
	movq %r10, TDX_MODULE_r10(%r12)
	movq %r11, TDX_MODULE_r11(%r12)

.Lno_output_struct:
	/* Restore the state of R12 register */
	pop %r12
.endm
